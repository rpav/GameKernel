cmake_minimum_required(VERSION 3.11)

if(CMAKE_CXX_COMPILER_ID STREQUAL "Clang")
  if(WIN32 AND NOT MINGW)
    function(__cxx_feature_process)
      __cxx_feature_clang_cl(${ARGN})
    endfunction()
  else()
    if(CXX_STDLIB STREQUAL "libc++")
      function(__cxx_feature_process)
        __cxx_feature_clang_libcxx(${ARGN})
      endfunction()
    else()
      function(__cxx_feature_process)
        __cxx_feature_gnu(${ARGN})
      endfunction()
    endif()
  endif ()
elseif (CMAKE_CXX_COMPILER_ID STREQUAL "GNU")
  function(__cxx_feature_process)
    __cxx_feature_gnu(${ARGN})
  endfunction()
elseif (CMAKE_CXX_COMPILER_ID STREQUAL "MSVC")
  function(__cxx_feature_process)
    __cxx_feature_msvc(${ARGN})
  endfunction()
endif ()

function(target_cxx_features TARGET)
  foreach(feature IN LISTS ARGN)
    __cxx_feature_process(${TARGET} ${feature})
  endforeach()
endfunction()

function(target_cxx_std TARGET STD)
  set(flags OPTIONAL)
  set(oneValueArgs EXTENSIONS)
  cmake_parse_arguments(_ "${flags}" "${oneValueArgs}" "" ${ARGN})

  set_property(TARGET ${TARGET} PROPERTY CXX_STANDARD ${STD})
  if(_OPTIONAL)
    set_property(TARGET ${TARGET} PROPERTY CXX_STANDARD_REQUIRED OFF)
    set(_opt "Yes")
  else()
    set_property(TARGET ${TARGET} PROPERTY CXX_STANDARD_REQUIRED ON)
    set(_opt "No")
  endif()

  if(_EXTENSIONS)
    set_property(TARGET ${TARGET} PROPERTY CXX_EXTENSIONS ON)
    set(_ext "Yes")
  else()
    set_property(TARGET ${TARGET} PROPERTY CXX_EXTENSIONS OFF)
    set(_ext "No")
  endif()

  message(STATUS "${TARGET} c++std: ${STD} (Extensions: ${_ext}, Optional: ${_opt})")
endfunction()


function(__cxx_feature_clang_cl T F)
endfunction()

function(__cxx_feature_clang_libcxx T F)
  if(F STREQUAL "filesystem")
    if(CLANG_VERSION_MINOR LESS 9)
      target_link_libraries(${T} INTERFACE c++fs)
    endif()
  elseif(F STREQUAL "threads")
    target_compile_options(${T} PUBLIC "-pthread")
  endif()
endfunction()

function(__cxx_feature_gnu T F)
  if(F STREQUAL "filesystem")
    target_link_libraries(${T} INTERFACE stdc++fs)
  elseif(F STREQUAL "threads")
    target_compile_options(${T} PUBLIC "-pthread")
  endif()
endfunction()

function(__cxx_feature_msvc T F)
endfunction()
